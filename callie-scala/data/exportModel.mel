proc export( string $mesh, string $filename ){

	$file = `fopen $filename "w"`;

	$uvNames = `polyUVSet -q -auv $mesh`;

	// vertex
	int $vertexCount[] = `polyEvaluate -v $mesh`;
	//fprint $file ( "numVertices=" + $vertexCount[0] + "\n" );
	fprint $file ( "[" );

	for( $i=0;$i<$vertexCount[0];$i++){
		float $vert[]   = `getAttr ($mesh+".vt["+$i+"]")`;
		float $offset[] = `getAttr ($mesh+".pt["+$i+"]")`;

		$vert[0] += $offset[0];
		$vert[1] += $offset[1];
		$vert[2] += $offset[2];


		fprint $file ( "\t("+$vert[0]+","+$vert[1]+","+$vert[2]+")" );

		if($i+1<$vertexCount[0]){
		    fprint $file ( ",\n" );
		}
	}
	fprint $file " ]\n";

	// faces

	select -r $mesh;
	int $faceCount[] = `polyEvaluate -f`;
	//fprint $file ( "numFaces=" + $faceCount[0]+"\n" );
	fprint $file ( "{" );

	for( $i=0;$i<$faceCount[0];$i++){
		int $vCount =0;

		select -r ( $mesh+".f["+$i+"]");

		string $vinds[] = `polyInfo -fv`;

		string $split[];
		tokenize $vinds[0] $split;

		fprint $file "[";
		for($j=0;$j<(size($split)-2);$j++){
			fprint $file ( "\t\t"+$split[(2+$j)] ); // +","+$uvIndex+","+$nIndex+")"

			select -r ($mesh+".vtxFace["+$split[($j+2)]+"]["+$i+"]");
			float $norm[] = `polyNormalPerVertex -q -xyz`;

			fprint $file ( ":("+$norm[0]+","+$norm[1]+","+$norm[2]+")" );

			fprint $file ( ":[" );
			for($k=0;$k<size($uvNames);$k++){
				polyUVSet -cuv -uvSet $uvNames[$k] $mesh;

				string $tinds[] = `polyListComponentConversion -fvf -tuv ($mesh+".vtxFace["+$split[($j+2)]+"]["+$i+"]")`;

				string $buffer[];
				tokenize $tinds[0] "[]" $buffer;

				float $uvs[] = `getAttr ($mesh + ".uv[" +($buffer[1])+"]")`;

				if($k > 0) fprint $file ( "," );
				fprint $file ( "("+$uvs[0]+","+$uvs[1]+")" );
			}
			fprint $file ( "]" );

			if( $j+1 < size($split)-2 ){
			    fprint $file (",");
			}
			fprint $file ("\n");

		}
		fprint $file "]";

	}
	fprint $file "}\n";


	fclose $file;
}

export("pPlatonic2", "C:/Users/root/IdeaProjects/callie/callie-scala/src/main/resources/demo/box/sphere.mod");
export("pCube12", "C:/Users/root/IdeaProjects/callie/callie-scala/src/main/resources/demo/box/cube.mod");

export("pCylinder30", "C:/Users/root/IdeaProjects/callie/callie-scala/src/main/resources/demo/map/floor.mod");

export("pSphere5", "C:/Users/root/IdeaProjects/callie/callie-scala/src/main/resources/demo/char/base.mod");
export("polySurface115", "C:/Users/root/IdeaProjects/callie/callie-scala/src/main/resources/demo/char/hair.mod");
//export("pPlatonic1", "C:/Users/root/IdeaProjects/callie/callie-scala/src/main/resources/sphere.mod")
//export("pCylinder30", "C:/Users/root/IdeaProjects/callie/callie-scala/data/map/floor.mod")
